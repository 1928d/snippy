#!/usr/bin/env perl
use warnings;
use strict;
use FindBin;
use lib "$FindBin::RealBin/../perl5";
###LINE_FOR_BREW_CONDA###
use Snippy::Version;
use Data::Dumper;
use List::Util qw(min max);
use Fatal;

# . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
# Globals

my $VERSION = Snippy::Version->version;
my $EXE = $FindBin::RealScript;

# . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
# Options

my(@Options, $debug, $fasta, $haploid, $info_tags, $format_tags, $set_filter);
setOptions();

my @keep_info = split m/,/, $info_tags;
my @keep_format = split m/,/, $format_tags;

# . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
# Main

my @hdr;
my @vcf;

while (<ARGV>) {
  if (m/^#/) {
    # VCF header lines
    push @hdr, $_;
    if (m/^##fileformat=/) {
      push @hdr, qq{##INFO=<ID=OLDVAR,Number=R,Type=String,Description="Original REF,ALT before decomposition">\n};
    }
  }
  else {
    chomp;
    # CHROM POS ID REF ALT QUAL FILTER INFO FORMAT SAMPLE1 [SAMPLE2] ...
    my($chrom,$pos,$id,$ref,$alt,$qual,$filter,$info,$format,@sample) = split m/\t/;

    die "This script only works with single sample VCF files" if @sample > 1;
    
    my $itag = decode_info($info);
    my $ftag = decode_format($format, $sample[0]); # assume only 1 sample
  
    my $type = $itag->{TYPE};
 
    # reduce to minimal representation
    ($pos,$ref,$alt) = minimal_rep($pos, $ref, $alt) if $type !~ /^[ms]np$/;
    
#    print STDERR "COMPLEX > $chrom\t$pos\t$ref\t$alt\n" if $debug;
#    print STDERR "COMPLEX < $chrom\t$pos\t$ref\t$alt\n" if $debug;
#    print STDERR "\n";
      
    # filter out heterozygous sites
    if ($haploid) {
      !exists $ftag->{GT} and die "No GT tag found in $format $sample[0]";
      next if $ftag->{GT} =~ m/0/ and $ftag->{GT} =~ /1/;
      $ftag->{GT} = substr($ftag->{GT}, -1, 1);
    }

    # this is samtools tag for ins/del
    $itag->{INDEL} = '' if $type =~ m/^(ins|del)$/;
    # ^^ BUG:  INDEL=; appears not INDEL;
    
    $filter = $set_filter if $set_filter;  # eg. "PASS"
    
    ($format,$sample[0]) = recode_format($ftag, @keep_format);

    if ($type eq 'complex') {
      my $L = max( length($ref), length($alt) );
      my $gaps = '-'x$L;
      $itag->{OLDVAR} = "$type,$pos,$ref,$alt";
      my $r0 = '';
      my $a0 = '';
      for my $i (0 .. $L-1) {
        my $r = substr($ref.$gaps, $i, 1);
        my $a = substr($alt.$gaps, $i, 1);
#        print STDERR "## complex [$i/$L] $ref=>$alt | $r => $a\n";
        next if $r eq $a;
        if ($r ne '-' and $a ne '-') { $itag->{TYPE} = 'snp'; }
        elsif ($r eq '-') { $itag->{TYPE} = 'ins'; $r = $r0 ; $a = $r0.substr($alt,$i,$L-$i+1); $pos--; }
        elsif ($a eq '-') { $itag->{TYPE} = 'del'; $r = $r0.substr($ref,$i,$L-$i+1) ; $a = $a0 ; $pos++; } 
        else { die "Unexpected variant $r:$a from $ref,$alt position $i" }
        $info = recode_info($itag, @keep_info, 'OLDVAR');
        push @vcf, [ $chrom,$pos+$i,$id,$r,$a,$qual,$filter,$info,$format,@sample ];
        $r0 = $r;
        $a0 = $a;
      }
    }
    else {   
      $info = recode_info($itag, @keep_info);
      push @vcf, [ $chrom,$pos,$id,$ref,$alt,$qual,$filter,$info,$format,@sample ];
    }
  }
}

# sort VCF as coordinates can overlap when decompsing key = chr(ALPHA),pos(NUMERIC)
@vcf = sort { $a->[0] cmp $b->[0] || $a->[1] <=> $b->[1] } @vcf;

# output VCF
print @hdr;
for my $row (@vcf) {
  print join("\t", @$row),"\n";
}

#----------------------------------------------------------------------

sub recode_format {
  my($kv, @keep) = @_;
  @keep = keys %$kv unless @keep;
  my $format = join(':', @keep);
  my $sample = join(':', map { $kv->{$_} } @keep);
  return ($format, $sample);
}

#----------------------------------------------------------------------

sub decode_format {
  my($format, $sample) = @_;
  my @fmt = split m/:/, $format;
  my @sam = split m/:/, $sample;
  die "Mismatch between FORMAT=$format and SAMPLE=$sample" if @fmt != @sam;
  my $kv = { map { ( $fmt[$_] => $sam[$_] ) } (0 .. $#fmt) };
  return $kv;
}

#----------------------------------------------------------------------

sub recode_info {
  my($kv, @keep) = @_;
  @keep = keys %$kv unless @keep;
  return join(';', map { "$_=".$kv->{$_} } @keep);
}

#----------------------------------------------------------------------

sub decode_info {
  my($info) = @_;
  my $kv;
  for my $pair (split m{;}, $info) {
    my($k,$v) = split m{=}, $pair;
    $kv->{$k} = $v;
  }
  return $kv;
}

#----------------------------------------------------------------------

sub minimal_rep {
 my($pos, $ref, $alt) = @_;
 my @ref = split m//, $ref;
 my @alt = split m//, $alt;
 while (@ref > 1 and @alt > 1 and $ref[-1] eq $alt[-1]) {
   pop @ref;
   pop @alt;
   print STDERR "  RIGHT : ref=[@ref] alt=[@alt] pos=$pos\n" if $debug;
 }
 while (@ref > 1 and @alt > 1 and $ref[0] eq $alt[0]) {
   shift @ref;
   shift @alt;
   $pos++;
   print STDERR "   LEFT : ref=[@ref] alt=[@alt] pos=$pos\n" if $debug;
 }
 return ( $pos, join('', @ref), join('', @alt) );
}

#----------------------------------------------------------------------
sub show_version {
  print "$EXE $VERSION\n";
  exit(0);
}

#----------------------------------------------------------------------
# Option setting routines

sub setOptions {
  use Getopt::Long;

  @Options = (
    {OPT=>"help!",     VAR=>\&usage,                DESC=>"This help"},
    {OPT=>"version!",  VAR=>\&show_version,         DESC=>"Print version and exit"},
    {OPT=>"debug+",    VAR=>\$debug,   DEFAULT=>0,  DESC=>"Output verbose debug info"},
    {OPT=>"ref=s",     VAR=>\$fasta,   DEFAULT=>'', DESC=>"FASTA reference"},
    {OPT=>"haploid!",  VAR=>\$haploid, DEFAULT=>0,  DESC=>"Convert to haploid genotypes"},
    {OPT=>"info-tags=s",   VAR=>\$info_tags,   DEFAULT=>'TYPE,DP,RO,AO,AF',  DESC=>"Keep these INFO tags"},
    {OPT=>"format-tags=s", VAR=>\$format_tags, DEFAULT=>'GT,DP,RO,AO,QR,QA',  DESC=>"Keep these FORMAT tags"},
    {OPT=>"filter=s",      VAR=>\$set_filter,  DEFAULT=>'',    DESC=>"Set FILTER to this"},
  );

  (!@ARGV) && (usage());

  &GetOptions(map {$_->{OPT}, $_->{VAR}} @Options) || usage();

  # Now setup default values.
  foreach (@Options) {
    if (defined($_->{DEFAULT}) && !defined(${$_->{VAR}})) {
      ${$_->{VAR}} = $_->{DEFAULT};
    }
  }
}

sub usage {
  print "SYNOPSIS\n  Decompose complex alleles into snp/mnp/ins/del\n";
  print "USAGE\n";
  print "  $EXE [options] in.vcf > out.vcf";
  print "OPTIONS\n";
  foreach (@Options) {
    printf "  --%-13s %s%s.\n",$_->{OPT},$_->{DESC},
           defined($_->{DEFAULT}) ? " (default '$_->{DEFAULT}')" : "";
  }
  exit(0);
}
 
#----------------------------------------------------------------------
