#!/usr/bin/env perl
use warnings;
use strict;
use FindBin;
use lib "$FindBin::RealBin/../perl5";
###LINE_FOR_BREW_CONDA###
use Snippy::Version;
use List::Util qw(max);
use Fatal;

my(@Options, $debug, $auto, $vcf, $ref, $bam, $html);
setOptions();

# . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
# Globals

my $VERSION = Snippy::Version->version;
my $EXE = $FindBin::RealScript;

# . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
# Options

if ($auto) {
  $vcf ||= 'snps.vcf';
  $ref ||= 'reference/ref.fa';
  $bam ||= 'snps.bam';
}

$vcf or die "need --vcf <snps.vcf>";
$ref or die "need --ref <ref.fa>";
$bam or die "need --bam <aln.bam>";

my $opt = $html ? '-d H' : '-d T';

# . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .
# Main

print STDERR "Parsing: $vcf\n";
open VCF, '<', $vcf;
while (<VCF>) {
  next if m/^#/;
  chomp;

  # [VCF] CHROM POS ID REF ALT QUAL FILTER INFO FORMAT unknown
  my($chr,$pos,undef,$refb,$alt,$qual) = split m/\t/;

  my $desc = "$chr  $pos  $refb => $alt  Q=$qual";
  my $begin = max(1, $pos-40); # put our SNP in the centre of the screen

  if ($html) {
    print "<HR>\n<H1>$desc</H1>\n";
    print "<P><TT>$</TT></P>\n";
  }
  else {
    print "\n\n\n", "-"x80, "\n$desc\n\n";
  }

  my $cmd = "samtools tview -p '$chr:$begin' $opt $bam $ref";
  print STDERR "Running: $cmd ...\n";
  system($cmd)==0 or die "Could not run: $cmd";
}

#----------------------------------------------------------------------
sub show_version {
  print "$EXE $VERSION\n";
  exit(0);
}

#----------------------------------------------------------------------
# Option setting routines

sub setOptions {
  use Getopt::Long;

  @Options = (
    {OPT=>"help!",     VAR=>\&usage,                DESC=>"This help"},
    {OPT=>"version!",  VAR=>\&show_version,         DESC=>"Print version and exit"},
    {OPT=>"debug!",    VAR=>\$debug,   DEFAULT=>0 , DESC=>"Output verbose debug info"},
    {OPT=>"auto!",     VAR=>\$auto,    DEFAULT=>0 , DESC=>"Autoset --vcf/bam/ref to snippy names"},
    {OPT=>"vcf=s",     VAR=>\$vcf,     DEFAULT=>'', DESC=>"VCF input file (raw)"},
    {OPT=>"bam=s",     VAR=>\$bam,     DEFAULT=>'', DESC=>"BAM alignments (indexed)"},
    {OPT=>"ref=s",     VAR=>\$ref,     DEFAULT=>'', DESC=>"FASTA reference (indexed)"},
    {OPT=>"html!",     VAR=>\$html,    DEFAULT=>0 , DESC=>"Write a HTML report instead of TXT"},
  );

  (!@ARGV) && (usage());

  &GetOptions(map {$_->{OPT}, $_->{VAR}} @Options) || usage();

  # Now setup default values.
  foreach (@Options) {
    if (defined($_->{DEFAULT}) && !defined(${$_->{VAR}})) {
      ${$_->{VAR}} = $_->{DEFAULT};
    }
  }
}

sub usage {
  select STDERR;
  print "SYNOPSIS\n  Convert a VCF (haploid) into TSV with column breakdown\n";
  print "USAGE\n";
  print "  $EXE [options] --auto > snps.txt  # from a snippy folder\n";
  print "  $EXE [options] --vcf snps.vcf --bam aln.bam --ref ref.fa > snps.txt\n";
  print "  $EXE [options] --vcf snps.vcf --bam aln.bam --ref ref.fa --html > snps.html\n";
  print "OPTIONS\n";
  foreach (@Options) {
    printf "  --%-13s %s%s.\n",$_->{OPT},$_->{DESC},
           defined($_->{DEFAULT}) ? " (default '$_->{DEFAULT}')" : "";
  }
  exit(1);
}
 
#----------------------------------------------------------------------
